plugins {
    alias libs.plugins.fabric.loom
    alias libs.plugins.legacy.looming
    id "maven-publish"
}

version = "$mod_version+$target_version"
group = maven_group

base {
    archivesName = archives_name
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    minecraft libs.minecraft
    mappings legacy.yarn(libs.versions.minecraft.get(), libs.versions.yarn.build.get())
    modImplementation libs.fabric.loader
    include(modImplementation libs.fabric.asm.get())
    include(implementation libs.zt.zip.get())
    include(runtimeOnly libs.slf4j.api.get())
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(8)
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    from("LICENSE")
}

publishing {
    publications.create("mavenJava", MavenPublication) {
        from components.java
    }
    repositories {}
}
